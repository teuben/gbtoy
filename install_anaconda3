#! /bin/csh -f
#
#  Install an anaconda3 (or miniconda3) python. A full install takes about 2 mins,
#  assuming you have the .sh file
#
#  An alternative is to set up a special conda environment for NEMO, e.g.
#
#     conda create -n nemo1 python=x.x -y
#     source activate nemo1
#     conda install -n nemo1 [package]
#     source deactivate
#     conda remove -n nemo1 -all
#
#  but this has not been experimented with.
#
#  May-2020        os=Linux/MacOSX
#  Nov-2020        for lmtoy

set url   = https://repo.continuum.io/miniconda/Miniconda3-latest-Linux-x86_64.sh
set url   = https://repo.anaconda.com/archive/Anaconda3-2020.07-MacOSX-x86_64.sh
set url   = https://repo.anaconda.com/archive/Anaconda3-2020.07-Linux-x86_64.sh
set url   = https://repo.anaconda.com/archive/Anaconda3-2020.11-%s-x86_64.sh
set url   = https://repo.anaconda.com/archive/Anaconda3-2021.04-%s-x86_64.sh
#set url   = https://repo.anaconda.com/archive/Anaconda3-2021.11-%s-x86_64.sh      # sphinx

set dir   = `pwd`/anaconda3
set wget  = wget
set os    = `uname -s`

# you better know what you are doing here
foreach arg ($*)
  set $arg
end

if ($os == Darwin) set os = MacOSX

set mc3 = `printf $url $os`
set msh = $mc3:t

$wget $mc3
bash $msh -b -p $dir

setenv PATH "$dir/bin:$PATH"

conda update -y conda
pip3 install --upgrade pip

# write the source-able shell startup files

echo "set path = ($dir/bin "'$path); rehash'  >> python_start.csh
echo "export PATH=${dir}/bin:"'$PATH'         >> python_start.sh

echo "Created python_start.[c]sh files for you"


