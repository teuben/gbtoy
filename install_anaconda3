#! /bin/csh -f
#
#  Install an anaconda3 python for GBT work.
#
#  This is optional, you can also try to install packages via conda virtual environments
#  or likewise operations. Be sure it's python3 though, as we will only support python3.
#
#  TBD: despite what it might say at moment below, it is adviced to install packages
#  using the "pip install -e" method, since we are likely to hack in them.
#  Certainly do not use "python setup.py".
#

            # ~670MB
set mc3   = https://repo.anaconda.com/archive/Anaconda3-2019.03-Linux-x86_64.sh
set msh   = $mc3:t
set dir   = `pwd`/anaconda3
set e     = 0

# you better know what you are doing here
foreach arg ($*)
  set $arg
end

if (-d anaconda3) then
  echo "anaconda3 is already installed. Remove this directory if you want to refresh it,"
  echo "or 'make conda' to update it"
  exit 0
endif

if ($e == 0) then
   set eopt = ()
else
   set eopt = (-e)
endif   

if (! -e $msh) wget $mc3
bash $msh -b -p $dir

setenv PATH "$dir/bin:$PATH"

conda update -y conda
pip install --upgrade pip


echo "set path = ($dir/bin "'$path); rehash'  >> python_start.csh
echo "export PATH=${dir}/bin:"'$PATH'         >> python_start.sh

# if your installed (some of these) via the Makefile, you will get them
# installed from source. Consider using the -e option
pip install $eopt specutils
pip install $eopt pyspeckit

# general things we need
pip install blessings     # for gbt-pipeline


# Optional things, arguably useful for hacking, commented out for sanity?

# conda install glueviz
